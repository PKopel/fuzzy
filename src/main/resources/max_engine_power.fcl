FUNCTION_BLOCK engine_power
    VAR_INPUT
        light_intensity : REAL;
        time_of_day : REAL;
        driver_weight: REAL;
        humidity: REAL;
    END_VAR
    VAR_OUTPUT
        max_engine_power : REAL;
    END_VAR

    FUZZIFY light_intensity
        TERM dim := (20, 1) (150, 1) (200, 0.50) (300, 0);
        TERM medium := (150, 0) (200, 0.50) (300, 1) (1000, 1) (1500, 0.50) (2000, 0);
        TERM bright := (1000, 0) (1500, 0.50) (2000, 1) (5000, 1);
    END_FUZZIFY

    FUZZIFY time_of_day
        TERM morning := (4, 0) (5, 0.50) (6, 1) (7, 1) (9, 0.50) (11, 0);
        TERM noon := (7, 0) (9, 0.50) (11, 1) (14, 1) (16, 0.50) (18, 0);
        TERM evening := (14, 0) (16, 0.50) (18, 1) (20, 1) (21, 0.50) (22, 0);
        TERM night := (0,1) (4, 1) (5, 0.50) (6, 0) (20, 0) (21, 0.50) (22, 1) (23, 1);
    END_FUZZIFY

    FUZZIFY driver_weight
        TERM light := (0, 1) (45, 1) (60, 0);
        TERM medium := (45, 0) (60, 1) (90, 1) (105, 0);
        TERM heavy := (90, 0) (105, 1) (150, 1);
    END_FUZZIFY

    FUZZIFY humidity
        TERM dry := (0, 1) (20, 1) (40, 0);
        TERM medium := (20, 0) (40, 1) (60, 1)  (80, 0);
        TERM wet := (60, 0) (80, 1) (100, 1);
    END_FUZZIFY

    DEFUZZIFY max_engine_power
        TERM low := (0, 1) (20, 1) (30, 0) (70, 0);
        TERM medium := (0,0) (20, 0) (30, 1) (45,1) (55, 0) (70, 0);
        TERM high := (0, 0) (45, 0) (55, 1) (70, 1);
        METHOD : COG;
        DEFAULT := NC;
    END_DEFUZZIFY

    RULEBLOCK first
        AND: MIN;
        ACT: MIN;

        RULE 0: IF (light_intensity IS dim OR time_of_day IS night)
                    AND (driver_weight IS light OR driver_weight IS medium)
                THEN max_engine_power IS low;
        RULE 1: IF (light_intensity IS dim OR time_of_day IS night)
                    AND driver_weight IS heavy
                THEN max_engine_power IS medium;
        RULE 2: IF (light_intensity IS medium OR (time_of_day IS morning OR time_of_day IS evening))
                    AND (driver_weight IS light OR driver_weight IS medium)
                THEN max_engine_power IS medium;
        RULE 3: IF (light_intensity IS medium OR time_of_day IS morning OR time_of_day IS evening)
                    AND driver_weight IS heavy
                THEN max_engine_power IS high;
        RULE 4: IF light_intensity IS bright
                    AND driver_weight IS light
                THEN max_engine_power IS medium;
        RULE 5: IF light_intensity IS bright
                    AND (driver_weight IS medium OR driver_weight IS heavy)
                THEN max_engine_power IS high;

        RULE 6: IF humidity IS dry
                    AND light_intensity IS bright
                THEN max_engine_power IS high;
        RULE 7: IF humidity IS dry
                    AND (light_intensity IS medium OR light_intensity IS dim)
                THEN max_engine_power IS medium;
        RULE 8: IF humidity IS medium
                    AND light_intensity IS bright
                THEN max_engine_power IS high;
        RULE 9: IF humidity IS medium
                    AND light_intensity IS medium
                THEN max_engine_power IS medium;
        RULE 10: IF humidity IS medium
                    AND light_intensity IS dim
                 THEN max_engine_power IS low;
        RULE 11: IF humidity IS wet
                    AND light_intensity IS bright
                 THEN max_engine_power IS medium;
        RULE 12: IF humidity IS wet
                    AND (light_intensity IS medium OR light_intensity IS bright)
                 THEN max_engine_power IS low;
    END_RULEBLOCK
END_FUNCTION_BLOCK